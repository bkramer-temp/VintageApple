   10 REM  TRAILS: EAT MORSELS AND OUTLAST YOUR OPPONENT!
   16 P0= PDL (0)-10:Q0=P0+20:P1= PDL (1)-10:Q1=P1+20
   20 DIM DX(1),DY(1):DX(0)=1:DY(0)=0:DX(1)=-1:DY(1)=0
   21 DIM X(1),Y(1):X(0)=5:Y(0)=20:X(1)=34:Y(1)=20
   23 DIM B(1),E(1):B(0)=0:B(1)=0:E(0)=0:E(1)=0
   24 DIM L(1):L(0)=5:L(1)=5
   25 DIM T(200)
   26 K=0
   30 GR 
   31 COLOR= 15
   32 HLIN 0,39 AT 0: HLIN 0,39 AT 39: VLIN 0,39 AT 0: VLIN 0,39 AT 39
   33 FOR N=1 TO 10: GOSUB 90: NEXT N
   40 FOR P=0 TO 1
   41 Z=0: GOSUB 200: IF P THEN GOTO 43
   42 I= PEEK (-16384): GOTO 44
   43 I=K:K=0
   44 IF I<160 THEN GOTO 65
   45 IF I=160 THEN GOTO 64
   46 IF I=201 THEN GOTO 59
   47 IF I=202 THEN GOTO 55
   48 IF I=203 THEN GOTO 61
   49 IF I=204 THEN GOTO 57
   50 GOTO 65
   55 IF DX(P)=1 THEN GOTO 65
   56 DX(P)=-1:DY(P)=0: GOTO 65
   57 IF DX(P)=-1 THEN GOTO 65
   58 DX(P)=1:DY(P)=0: GOTO 65
   59 IF DY(P)=1 THEN GOTO 65
   60 DX(P)=0:DY(P)=-1: GOTO 65
   61 IF DY(P)=-1 THEN GOTO 65
   62 DX(P)=0:DY(P)=1: GOTO 65
   63 IF L(P)<=5 THEN GOTO 65
   64 L(P)=L(P)-1:Z=2
   65 FOR M=0 TO Z: GOSUB 200
   66 U=X(P)+DX(P):V=Y(P)+DY(P):X(P)=U:Y(P)=V:C= SCRN( U,V)
   67 IF C<>13 OR L(P)>=100 THEN GOTO 69
   68 L(P)=L(P)+5: GOTO 70
   69 IF C THEN GOTO 100
   70 COLOR= P+2: PLOT U,V
   71 T(P*100+E(P))=40*V+U:E(P)=E(P)+1: IF E(P)=100 THEN E(P)=0
   73 J=E(P)-B(P): IF J<0 THEN J=J+100
   74 IF J<=L(P) THEN GOTO 78
   75 T=T(P*100+B(P)):B(P)=B(P)+1: IF B(P)=100 THEN B(P)=0
   76 V=T/40:U=T-V*40: COLOR= 0: PLOT U,V
   77 J=J-1: GOTO 74
   78 NEXT M
   79 IF RND (100)<5 THEN GOSUB 90
   80 NEXT P: GOTO 40
   90 U= RND (36)+2:V= RND (36)+2: IF SCRN( U,V) THEN GOTO 90
   91 COLOR= 13: PLOT U,V: RETURN 
  100 FOR K=0 TO 70: COLOR= P+2: PLOT U,V: COLOR= 15: PLOT U,V: NEXT K: END 
  200 IF PEEK (-16287)>127 THEN K=160
  201 IF K=160 THEN RETURN 
  202 I= PDL (0)
  203 IF I>P0 THEN GOTO 205
  204 K=202: RETURN 
  205 IF I<Q0 THEN GOTO 207
  206 K=204: RETURN 
  207 I= PDL (1)
  208 IF I>P1 THEN GOTO 210
  209 K=201: RETURN 
  210 IF I<Q1 THEN RETURN 
  211 K=203: RETURN 
